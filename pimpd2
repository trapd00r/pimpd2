#!/usr/bin/perl
use vars qw($VERSION);
my $APP;

$APP     = 'pimpd2';
$VERSION = '0.15';

use strict;

use App::Pimpd;
use App::Pimpd::Player;
use App::Pimpd::Info;
use App::Pimpd::Shell;
use App::Pimpd::Commands;
use App::Pimpd::Transfer;
#use App::Pimpd::Validate;
use App::Pimpd::Collection::Search;
use App::Pimpd::Collection::Album;
use App::Pimpd::Playlist;
use App::Pimpd::Playlist::Search;
use App::Pimpd::Playlist::Favorite;
use App::Pimpd::Playlist::Randomize;

use Term::ExtendedColor;
use Getopt::Long;
use Pod::Usage;
use Data::Dumper;

@ARGV or usage();

GetOptions(
  'np|now-playing'       => sub { print current(), "\n"; },
  'i|info'               => \&info,
  'lsa|songs'            => sub { _lsa(@ARGV); },

  'l|laa|albums'         => sub {
    print "$_\n" for albums_by_artist(join(' ',@ARGV));
  },

  'randomize'            => sub { print "$_\n" for randomize(@ARGV); },
  'ra|random-album'      => sub { print "$_\n" for randomize_albums(@ARGV); },
  'rt|random-track'      => sub { _rt(); },

  'af|add'               => sub { _add_files(@ARGV); },
  'aa|add-album'         => sub { @_ = map{$_->file} songs_on_album; add(@_); },
  'a|apl|add-playlist'   => sub { add_playlist(@ARGV); },
  'lsp|playlists'        => sub { print "$_\n" for list_all_playlists(); },
  'pls|playlist'         => sub { show_playlist(); },

  'p|play'               => sub { play(); },
  's|stop'               => sub { stop(); },
  'k|kill'               => sub { player_destruct(); },
  'sh|shell'             => sub { \&spawn_shell(@ARGV); },
  'rma|rmalbum'          => sub { remove_album_from_playlist(@ARGV); },

  'cp'                   => sub { cp(@ARGV ? @ARGV : $target_directory) },
  'cpa'                  => sub { cp_album(@ARGV ? @ARGV : $target_directory) },

  'fav|favorite|love'    => sub { add_to_favlist(@ARGV); }, # FIXME
  'loved'                => sub {
    if(already_loved($mpd->current->file)) {
      printf("%s, %s by %s is loved.\n",
        fg('bold', 'Yes'),
        fg($c[10], $mpd->current->title),
        fg($c[2],  fg('bold', $mpd->current->artist)),
      );
    }

    else {
      printf("%s, %s by %s is not loved yet.\n",
        fg('bold', 'No'),
        fg($c[10], $mpd->current->title),
        fg($c[2],  fg('bold', $mpd->current->artist)),
      );
    }
  },

  'sip|external'         => sub { songs_in_playlist(@ARGV); },

  'slove'                => sub {
    my @files = search_favlist(@ARGV);
    print "$_\n" for @files;
  },

  'spl|search-playlist'  => sub { _search_playlist(@ARGV); },
  'sap|search-all-pls'   => sub { search_all_playlists(@ARGV); },
  'sdb|search-db'        => sub { print "$_\n" for search_db_quick(@ARGV); },

  'sar|search-artist'    => sub { print "$_\n" for search_db_artist(@ARGV); },
  'sal|search-album'     => sub { print "$_\n" for search_db_album(@ARGV); },
  'set|search-title'     => sub { print "$_\n" for search_db_title(@ARGV); },

  'next'                 => sub { next_track(); },
  'prev'                 => sub { previous_track(); },
  'clear'                => sub { clear_playlist(); },
  'crop'                 => sub { crop(@ARGV); },
  'crossfade|xfade'      => sub { crossfade(@ARGV); },
  'pause'                => sub { toggle_pause; },
  'repeat'               => sub { toggle_repeat; },
  'random'               => sub { toggle_random; },

  'debug'                => sub { _debug(); },
  'help'                 => \&usage,
  'version'              => sub { print "$APP v$VERSION\n" and exit 0; },
  'man'                  => sub { pod2usage( verbose => 3 ); },
);

sub _rt {
  play_pos_from_playlist(random_track_in_playlist());
  print current(), "\n";
}

# Wrapper for songs_on_album()
sub _lsa {
  my($album, $artist) = @_;

  $album or $album = $mpd->current->album;
  print $_->file, "\n" for songs_on_album($album, $artist);
}

sub _search_playlist {
  my $result = search_playlist(@_); # hashref

  if( keys(%{$result}) < 2 ) {
    play_pos_from_playlist( keys(%{$result}) );
  }
  queue(keys(%{$result})); #FIXME queue should take arrayref
}


sub _add_files {
  my @files = @_;

  if(!-t STDIN) {
    while(<STDIN>) {
      push(@files, $_);
    }
  }
  add_to_playlist(@files);
}

sub usage() {
  print "$APP v$VERSION\n\n";
  pod2usage(verbose => 1);
  exit 0;
}

sub _debug {
  my $i = 0;
  for(@c) {
    print fg($_, "\$c[$i]"), "\n";
    $i++;
  }
}

=pod

=head1 NAME

pimpd2 - Perl Interface for the Music Player Daemon 2

=head1 DESCRIPTION

pimpd2 is a command-line based MPD client that implements all the features
the author was missing from the other awesome client, mpc.

=head1 FEATURES

=over 8

=item -np, --now-playing

Show basic song information on a single line.

=item -i, --info

Show all available song information and MPD server status.

=item -cp, --copy

Copy the currently playing song to destination.

If destination is omitted, uses the C<$target_directory> variable from the
configuration file.

=item -cpa, --copy-album

Copy the currently playing album to destination.

If destination is omitted, uses the C<$target_directory> variable from the
configuration file.

=item -sh, --shell

Spawn the interactive pimpd2 shell.

All the regular features can be used. Commands that return data that can be
added to the current playlist will do so automagically for convenience, since
we can not read from standard input while being interactive.

=item -q, --queue

Queue tracks. Arguments must be valid playlist position IDs as shown in the
C<--playlist> output.

You can also use the C<--search-playlist> command if the tracks to be queued
follows a pattern.

=back

=head2 PLAYLIST INTERACTION

=over 8

=item -pls, --playlist

Show the current playlist.

=item -lsp, --playlists

List all playlists known by MPD.

=item -af, --add-files

Add files to the current playlist.

Can read from standard input:

  pimpd2 --randomize 42 Nirvana | pimpd2 -af

Accepts file arguments:

  pimpd2 -af ~/music/Nirvana/Bleach/*.flac

=item -a, --add-playlist

Add playlist to the current playlist.

If not given a full name, and the name partially matches existing playlists,
prompts for input:

  pimpd2 -a 2010

    0 2010-12-indie
    1 2010-12-other
    2 2010-12-pop
    3 2010-12-punk_rock
    4 2010-12-rock
    5 2010-12-undef
  choice:

If choice equals 'all', all matching playlists are added.

=item -r, --randomize

Return B<n> random songs from the collection.
The first argument is the number of songs, the second argument is an optional
artist name. If an artist name is specified, will only return random songs from
that particular artist.

If no arguments are specified, returns 100 random songs.

If you want to add the results to the current playlist, pipe it to C<--add-files>:

  pimpd2 -r 12 | pimpd2 -af

Or use the interactive shell ( C<-sh> ) which does this for you automatically.

=item -ra, --random-album

Return B<n> random full albums.

A pipe to C<--add-files> will add the results to the current playlist.

=item -rma, --rmalbum

Given a string, searches the current playlist for matching albums, and remove
them from the playlist. The string can be a regular expression.

=item -f, --love

Favorize, or love, the current song.

If called with zero arguments, the song will be saved to a playlist following
this naming scheme:

  %year-%month-%genre.m3u

Else, the argument is used for the playlist name, thus:

  pimpd2 --love lovesongs

adds the song to lovesongs.m3u

=item --loved

Check if the currently playing song is already loved or not.

=item -sip, --external

List songs in external playlists. The logic from C<--add-playlist> is used here
as well:

  pimpd2 -sip 2010

    0 2010-12-indie
    1 2010-12-other
    2 2010-12-pop
    3 2010-12-punk_rock
    4 2010-12-rock
    5 2010-12-undef
  Choice:

thus 'all' or simply hitting enter will list all songs in all playlists
matching '2010'.

=item --slove

Search the database with loved songs for PATTERN.

If PATTERN is omitted, returns all loved songs.

=item -spl, --search-playlist

Search the current playlist for string, possibly a regular expression.

If more then one song is found, queues up the results. See C<--queue>.

=back

=head2 COLLECTION INTERACTION

=over 8

=item -lsa, --songs

List all songs on album.

If no argument is specified, use the album tag from the currently playing song.

A pipe to C<--add-files> will add the results to the current playlist:

  pimpd2 -lsa Stripped | pimpd2 -af

=item -l, --albums

List all albums where artist is featured.

If no argument is specified, use the artist tag from the currently playing song.

=item -sdb, --search-db

Search the database for string, possibly a regular expression.

A pipe to C<--add-files> will add the results to the current playlist:

=item -sar, --search-artist

Search the database for artist.

A pipe to C<--add-files> will add the results to the current playlist:

=item -sal, --search-album

Search the database for album.

A pipe to C<--add-files> will add the results to the current playlist:

=item -set, --search-title

Search the database for title.

A pipe to C<--add-files> will add the results to the current playlist:

=back

=head2 CONTROLS

=over 8

=item -n, --next

Play the next track in the playlist.

=item -p, --previous

Play the previous track in the playlist.

=item -cl, --clear

Clear the current playlist.

=item -cr, --crop

Remove all songs but the current one from the playlist.

=item -x, --xfade

Set crossfade.

=item --pause

Toggle playback status.

=item --repeat

Toggle repeat on/off

=item --random

Toggle random on/off

=item --play

Start playback.

If a remote stream URL and an external player is specified in the
configuration file, starts playback on the local machine as well as the
MPD server.

=item --stop

Stop playback, locally and remote.

=item --kill

Stop local playback.

=back

=head1 OPTIONS

  -np,   --now-playing      basic song info on a single line
  -i,    --info             full song info
  -cp,   --copy             copy the current track to destination
  -cpa,  --copy-album       copy the current album to destination
  -sh,   --shell            spawn the interactive shell
  -q,    --queue            queue tracks

=head2 Playlist

  -pls,  --playlist         show the current playlist
  -lsp,  --playlists        list all known playlists
  -af,   --add-files        add files to playlist
  -a,    --add-playlist     add playlist
  -r,    --randomize        randomize a new playlist with n tracks
  -ra,   --random-album     add n random full albums
  -rma,  --rmalbum          remove album matching pattern from playlist
  -f,    --love             love song
         --loved            check if the current song is loved
  -sip,  --external         list songs in playlists
  -spl,  --search-playlist  search the current playlist for str

=head2 Collection

  -lsa,  --songs            list songs on album
  -l,    --albums           list albums by artist
  -sdb,  --search-db        search database for pattern
  -sar,  --search-artist    search database for artist
  -sal,  --search-album     search database for album
  -set,  --search-title     search database for title
         --slove            search the database with loved songs for pattern

=head2 Controls

  -n,    --next             next track in playlist
  -p,    --previous         previous track in playlist
  -cl,   --clear            clear the playlist
  -cr,   --crop             remove all songs but the current one from playlist
  -x,    --xfade            set crossfade
         --pause            toggle playback status
         --repeat           toggle repeat mode
         --random           toggle random mode

  -p,    --play             start playback (locally and remote)
  -s,    --stop             stop playback (locally and remote)
  -k,    --kill             stop playback (locally)

  -h,    --help             show the help and exit
  -m,    --man              show the manual and exit
  -v,    --version          show version info and exit

=head1 ENVIRONMENT

pimpd2 will look for a configuration file in the following locations, in this
order:

  ~/.config/pimpd2/pimpd.conf
  ~/.pimpd2.conf
  ~/pimpd2.conf
  ./pimpd2.conf
  /etc/pimpd2.conf

=head1 AUTHOR

  Magnus Woldrich
  CPAN ID: WOLDRICH
  magnus@trapd00r.se
  http://japh.se

=head1 REPORTING BUGS

Report bugs and/or feature requests to <magnus@trapd00r.se>

=head1 COPYRIGHT

Copyright (C) 2010 Magnus Woldrich. All right reserved.
This program is free software; you can redistribute it and/or modify it under
the same terms as Perl itself.

=head1 SEE ALSO

pimpd2.conf(1), mpd(1)

=cut
